# Maintainer: XZS <d.f.fischer at web dot de>
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgbase='firefox-theme-adwaita-git'
pkgname=('firefox-theme-gnome-git' 'firefox-extension-gnome-theme-tweak-git')
pkgver=32.1
pkgrel=1
pkgdesc="Adwaita theme for Firefox (Matches the default Gnome Shell theme)"
url="https://addons.mozilla.org/firefox/addon/adwaita/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('unzip' 'zip')
source=("$pkgbase::git://github.com/gnome-integration-team/firefox-gnome.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgbase
  grep -Pom1 '(?<="version": ").*(?=")' config.json
}

build() {
  cd $pkgbase
  ./make-xpi.py
}

package_firefox-theme-gnome-git() {
  provides=('firefox-theme-adwaita' 'firefox-theme-gnome')
  conflicts=('firefox-theme-adwaita' 'firefox-theme-gnome')

  cd $pkgbase
  dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$(grep -Pom1 '(?<=<em:id>).*(?=</em:id>)' .build/theme/install.rdf)"

  install -d "$dstdir"
  unzip -d "$dstdir" gnome-firefox-theme.xpi
}

package_firefox-extension-gnome-theme-tweak-git() {
  pkgdesc='Extension for customizing GNOME theme.'
  depends=('firefox-theme-gnome')

  cd $pkgbase
  dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$(grep -Pom1 '(?<=<em:id>).*(?=</em:id>)' .build/extension/install.rdf)"

  install -d "$dstdir"
  unzip -d "$dstdir" gnome-firefox-extension.xpi
}
